plugins {
    id("com.gradleup.shadow") version "9.2.2"
    id("antlr")
}

dependencies {
    shadow("org.apache.commons:commons-math4-core:4.0-beta1")
    shadow("com.github.ben-manes.caffeine:caffeine:3.2.1")
    shadow("com.github.Chronoken:EffectLib:753da6c")
    shadow("co.aikar:acf-paper:0.5.1-SNAPSHOT")
    shadow("org.bstats:bstats-bukkit:3.0.2")
    shadow("com.github.ezylang:EvalEx:0dcb042")

    shadow("org.antlr:antlr4-runtime:4.13.2")
    antlr("org.antlr:antlr4:4.13.2")

    shadow(project(path: ":nms:shared", configuration: "apiElements"))
    shadow(project(path: ":nms:latest")) { transitive = false }

    implementation("com.github.retrooper:packetevents-spigot:2.10.1")
    implementation("com.comphenix.protocol:ProtocolLib:5.3.0") { transitive = false }
    implementation("me.libraryaddict.disguises:libsdisguises:10.0.44-SNAPSHOT") { transitive = false }
    implementation("net.milkbowl.vault:VaultAPI:1.7") { transitive = false }
    implementation("me.clip:placeholderapi:2.11.6") { transitive = false }
    implementation("com.github.GriefPrevention:GriefPrevention:17.0.0") { transitive = false }
    implementation("com.github.Xezard:XGlow:1.1.0") {
        exclude(module: "XGlowPlugin")
        exclude(module: "XGlowExample")
    }
    implementation("com.sk89q.worldguard:worldguard-core:7.1.0-SNAPSHOT") { transitive = false }
    implementation("com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT") { transitive = false }
    implementation("com.sk89q.worldedit:worldedit-core:7.4.0-SNAPSHOT") { transitive = false }
    implementation("com.sk89q.worldedit:worldedit-bukkit:7.4.0-SNAPSHOT") { transitive = false }

}

base {
    archivesName = "MagicSpells"
}

jar {
    manifest {
        attributes(
            "paperweight-mappings-namespace": "mojang"
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate("com.github.benmanes.caffeine", "com.nisovin.magicspells.shaded.com.github.benmanes.caffeine")
    relocate("org.apache.commons.math4", "com.nisovin.magicspells.shaded.org.apache.commons.math4")
    relocate("de.slikey.effectlib", "com.nisovin.magicspells.shaded.effectlib")
    relocate("co.aikar.commands", "com.nisovin.magicspells.shaded.acf")
    relocate("co.aikar.locales", "com.nisovin.magicspells.shaded.locales")
    relocate("org.bstats", "com.nisovin.magicspells.shaded.bstats")
    relocate("de.slikey.exp4j", "com.nisovin.magicspells.shaded.exp4j")
    relocate("com.ezylang.evalex", "com.nisovin.magicspells.shaded.evalex")

    // Included by apache/commons-math, copied by this project.
    exclude("META-INF/LICENSE")
    exclude("META-INF/NOTICE")
    // Include our own copies of attributions.
    from("$rootDir/3rd_party_licenses") {
        into "META-INF/3rd_party_licenses"
    }
    from("$rootDir/NOTICE.md") {into "META-INF"}
    from("$rootDir/LICENSE") {into "META-INF"}

    archiveClassifier.set("")
}

generateGrammarSource {
    packageName = "com.nisovin.magicspells.util.grammars"
    arguments += ["-visitor", "-no-listener"]
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
